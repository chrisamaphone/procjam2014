scene : type.
a1 : scene.
b2 : scene.
b3 : scene.
b4 : scene.
b5 : scene.
b6 : scene.
b7 : scene.
b8 : scene.
b9 : scene.
b10 : scene.
b11 : scene.
c12 : scene.
c13 : scene.
c14 : scene.
d15 : scene.
d16 : scene.
d17 : scene.
unknown : scene.

location : type.
sidehall      : location.
atrium        : location.
hall          : location.
oratorio      : location.
diningroom    : location.
luisaroom     : location.
dannunzioroom : location.
leda          : location.
above-atrium  : location.
finzi-office  : location.
offstage      : location.

%% characters
tamara    : scene -> location -> type.
luisa     : scene -> location -> type.
emilia    : scene -> location -> type.
finzi     : scene -> location -> type.
despiga   : scene -> location -> type.
aelis     : scene -> location -> type.
dannunzio : scene -> location -> type.
carlotta  : scene -> location -> type.
dante     : scene -> location -> type.
mario     : scene -> location -> type. % 10 characters.

charname : type.
'tamara : charname.
'luisa : charname.
'emilia : charname.
'finzi : charname.
de_Spiga : charname.
'aelis : charname.
d'Annunzio : charname.
'carlotta : charname.
'dante : charname.
'mario : charname.

eq : charname -> charname -> type.
% eq/i : eq C C.

%%%% SECTION A %%%%

a1_dante_finzi
: Pi Dante:charname. Pi Finzi:charname.
  dante a1 offstage * finzi a1 offstage 
  * eq Dante 'dante * eq Finzi 'finzi
  -o {dante a1 atrium * finzi a1 atrium}.

a1_emilia_enters
: Pi Emilia.
  emilia a1 offstage 
  * eq Emilia 'emilia
  -o {emilia a1 atrium}.

a1_dannunzio_enters
: Pi Dannunzio.
  eq Dannunzio d'Annunzio *
  dannunzio a1 offstage -o {dannunzio a1 atrium}.

a1_dannunzio_exits
: Pi Dannunzio.
  eq Dannunzio d'Annunzio *
  dannunzio a1 atrium -o {dannunzio a1 dannunzioroom}.

a1_emilia_exits
: Pi Emilia.
  eq Emilia 'emilia *
  emilia a1 atrium -o {emilia a1 sidehall}.

a1_aelis_enters
: Pi Aelis.
  eq Aelis 'aelis *
  aelis a1 offstage -o {aelis a1 atrium}.

a1_carlotta_enters
: Pi Carlotta.
  eq Carlotta 'carlotta *
  carlotta a1 offstage -o {carlotta a1 atrium}.

a1_carlotta_and_aelis_exit
: Pi Aelis. Pi Carlotta.
  eq Aelis 'aelis * eq Carlotta 'carlotta *
  aelis a1 atrium * carlotta a1 atrium
           -o {aelis a1 diningroom * carlotta b4 hall}.

a1_mario_enters
: Pi Mario.
  eq Mario 'mario *
  mario a1 offstage -o {mario a1 atrium}.

a1_mario_exits
: Pi Mario.
  eq Mario 'mario *
  mario a1 atrium -o {mario d15 atrium}.

a1_groups_split
: Pi Dante. Pi Finzi. Pi Despiga.
  eq Dante 'dante * eq Finzi 'finzi * eq Despiga de_Spiga *
  dante a1 atrium * finzi a1 atrium * despiga a1 atrium
       -o { dante a1 luisaroom
           * finzi a1 diningroom
           * despiga b2 atrium }.
%% at this point, the audience is divided and the story becomes truly
%% concurrent.

% Dante takes group 1 to Luisa and D'Annunzio, then heads to the atrium
% alone.
a1_grp1_luisa
: Pi Dante. Pi Luisa.
  eq Dante 'dante * eq Luisa 'luisa *
  dante a1 luisaroom * luisa a1 offstage
-o {dante a1 dannunzioroom * luisa b3 luisaroom}.

a1_grp1_dannunzio
: Pi Dante. Pi Dannunzio.
  eq Dante 'dante * eq Dannunzio d'Annunzio *
  dante a1 dannunzioroom * dannunzio a1 dannunzioroom
-o {dante b10 atrium * dannunzio b6 dannunzioroom}.

% Finzi takes group 2 to Aelis, then Emilia, then meets Carlotta in the
% hall.
a1_grp2_aelis
: Pi Finzi. Pi Aelis.
  eq Finzi 'finzi * eq Aelis 'aelis *
  finzi a1 diningroom * aelis a1 diningroom
-o {finzi a1 sidehall * aelis b9 diningroom}.

a1_grp2_emilia
: Pi Finzi. Pi Emilia.
  eq Finzi 'finzi * eq Emilia 'emilia *
  finzi a1 sidehall * emilia a1 sidehall
-o {finzi b4 hall * emilia b7 leda}.


%%%% SECTION B %%%%

b2_despiga_monologue
: Pi Despiga.
  eq Despiga de_Spiga *
  despiga b2 atrium -o {despiga b10 atrium}.

b3_luisa_monologue
: Pi Luisa.
  eq Luisa 'luisa *
  luisa b3 luisaroom -o {luisa b11 dannunzioroom}.

b4_carlotta_kisses_finzi
: Pi Finzi. Pi Carlotta.
  eq Finzi 'finzi * eq Carlotta 'carlotta *
  finzi b4 hall * carlotta b4 hall
-o {finzi b5 hall * carlotta b8 leda}.

b5_finzi_monologue
: Pi Finzi.
  eq Finzi 'finzi *
  finzi b5 hall -o {finzi c12 atrium}.

b6_dannunzio_monologue
: Pi Dannunzio.
  eq Dannunzio d'Annunzio *
  dannunzio b6 dannunzioroom -o {dannunzio b11 dannunzioroom}.

b7_emilia_monologue
: Pi Emilia.
  eq Emilia 'emilia *
  emilia b7 leda -o {emilia b8 leda}.

b8_emilia_carlotta
: Pi Emilia. Pi Carlotta.
  eq Emilia 'emilia * eq Carlotta 'carlotta *
  emilia b8 leda * carlotta b8 leda 
-o {emilia c12 diningroom * carlotta c12 atrium}.

b9_aelis_monologue
: Pi Aelis.
  eq Aelis 'aelis *
  aelis b9 diningroom -o {aelis c12 atrium}.

b10_dante_despiga
: Pi Dante. Pi Despiga.
  eq Dante 'dante * eq Despiga de_Spiga *
  dante b10 atrium * despiga b10 atrium
-o {dante c12 atrium * despiga c12 atrium}.

b11_dannunzio_luisa
: Pi Dan. Pi Luisa.
  eq Dan d'Annunzio * eq Luisa 'luisa *
  dannunzio b11 dannunzioroom * luisa b11 dannunzioroom
-o {dannunzio c12 atrium * luisa c12 above-atrium}.

%%%% SECTION C %%%%

c12_finzi_exit
: Pi Despiga. Pi Dante. Pi Aelis. Pi Finzi. Pi Carlotta. Pi Luisa.
  eq Despiga de_Spiga * eq Dante 'dante * eq Aelis 'aelis *
  eq Finzi 'finzi * eq Carlotta 'carlotta * eq Luisa 'luisa *
  despiga   c12 atrium
* dante     c12 atrium
* aelis     c12 atrium
* finzi     c12 atrium
* carlotta  c12 atrium
* luisa     c12 above-atrium
-o {  despiga   c12 atrium
    * dante     c12 atrium
    * aelis     c12 atrium
    * finzi     c12 finzi-office
    * carlotta  c12 atrium
    * luisa     c12 above-atrium}.

c12_gunshot
: Pi Luisa. Pi Finzi. Pi Emilia.
  eq Luisa 'luisa * eq Finzi 'finzi * eq Emilia 'emilia *
  luisa c12 above-atrium * finzi c12 finzi-office * emilia c12 diningroom
-o {luisa c12 atrium * finzi c12 atrium * emilia c12 atrium}.

c12_emilia_carlotta_aelis_luisa_leave_atrium
: Pi Emilia. Pi Carlotta. Pi Aelis. Pi Luisa.
  eq Emilia 'emilia * eq Carlotta 'carlotta * eq Aelis 'aelis * 
    eq Luisa 'luisa *
  emilia c12 atrium * carlotta c12 atrium * aelis c12 atrium 
  * luisa c12 atrium
-o {emilia c13 sidehall * carlotta c13 sidehall * aelis c13 sidehall
    * luisa c13 atrium}.

c13_finzi_luisa_confrontation
: Pi Finzi. Pi Luisa.
  eq Finzi 'finzi * eq Luisa 'luisa *
  finzi c12 atrium * luisa c13 atrium
-o {finzi d15 atrium * luisa c13 sidehall}.

c13_emilia_aelis_carlotta_luisa_sidehall
: Pi Emilia. Pi Aelis. Pi Carlotta.
  eq Emilia 'emilia * eq Aelis 'aelis * eq Carlotta 'carlotta *
  emilia c13 sidehall * aelis c13 sidehall * carlotta c13 sidehall
* luisa c13 sidehall
%% all exit back to atrium
-o {emilia c13 atrium * aelis c14 atrium * carlotta c13 atrium
* luisa c13 atrium}.

c12_take_presents_to_leda
: Pi Dante. Pi Carlotta. Pi Luisa.
  eq Dante 'dante * eq Carlotta 'carlotta * eq Luisa 'luisa *
  dante c12 atrium * carlotta c13 atrium * luisa c13 atrium 
* emilia c13 atrium
-o {dante c14 leda * carlotta c14 leda * luisa c14 leda
* emilia c14 atrium}.

c14_emilia_enter_leda
: Pi Dante. Pi Carlotta. Pi Luisa. Pi Emilia.
  eq Dante 'dante * eq Carlotta 'carlotta * eq Luisa 'luisa
    * eq Emilia 'emilia *
  dante c14 leda * carlotta c14 leda * luisa c14 leda * emilia c14 atrium
-o {dante c14 leda * carlotta c14 leda * luisa c14 leda * emilia c14 leda}.

c14_all_exit_leda
: Pi Dante. Pi Carlotta. Pi Luisa. Pi Emilia.
  eq Dante 'dante * eq Carlotta 'carlotta * eq Luisa 'luisa
    * eq Emilia 'emilia *
  dante c14 leda * carlotta c14 leda * luisa c14 leda * emilia c14 leda
-o {dante c14 atrium * carlotta c14 atrium * luisa c14 atrium 
    * emilia c14 atrium}.
%% n.b. technically they go "back" to scene c12, but this disrupts
%% a useful form of stratification...

c_final
: Pi Luisa. Pi Carlotta. Pi Aelis. Pi Despiga. Pi Dante. Pi Dannunzio.
  Pi Emilia.
  eq Luisa 'luisa * eq Carlotta 'carlotta * eq Aelis 'aelis *
    eq Despiga de_Spiga * eq Dante 'dante * eq Dannunzio d'Annunzio *
    eq Emilia 'emilia *
  luisa c14 atrium * carlotta c14 atrium * dante c14 atrium 
* emilia c14 atrium * aelis c14 atrium * dannunzio c12 atrium
* despiga c12 atrium
-o { luisa d17 oratorio * carlotta d17 oratorio
   * aelis d17 oratorio * despiga d17 oratorio
   * dante d15 atrium * dannunzio d15 atrium
   * emilia d15 diningroom }.

init : type =
{
Exists Tamara:charname. Exists Luisa:charname. Exists Emilia : charname. Exists Finzi : charname. Exists DeSpiga : charname. Exists Aelis : charname.
  Exists Dannunzio : charname. Exists Carlotta : charname. Exists Dante : charname. Exists Mario : charname.
  tamara    d15 atrium
* luisa     a1 offstage
* emilia    a1 offstage
* finzi     a1 offstage
* despiga   a1 atrium
* aelis     a1 offstage
* dannunzio a1 offstage
* carlotta  a1 offstage
* dante     a1 offstage
* mario     a1 offstage

* eq Tamara 'tamara
* eq Luisa 'luisa
* eq Emilia 'emilia
* eq Finzi 'finzi
* eq DeSpiga de_Spiga
* eq Aelis 'aelis
* eq Dannunzio d'Annunzio
* eq Carlotta 'carlotta
* eq Dante 'dante
* eq Mario 'mario}.

#trace * init.

#query * * * 10
Pi Tamara:charname. Pi Luisa:charname. Pi Emilia : charname. Pi Finzi : charname. Pi DeSpiga : charname. Pi Aelis : charname.
  Pi Dannunzio : charname. Pi Carlotta : charname. Pi Dante : charname. Pi Mario : charname.
  tamara    d15 atrium
* luisa     a1 offstage
* emilia    a1 offstage
* finzi     a1 offstage
* despiga   a1 atrium
* aelis     a1 offstage
* dannunzio a1 offstage
* carlotta  a1 offstage
* dante     a1 offstage
* mario     a1 offstage

* !eq Tamara 'tamara
* !eq Luisa 'luisa
* !eq Emilia 'emilia
* !eq Finzi 'finzi
* !eq DeSpiga de_Spiga
* !eq Aelis 'aelis
* !eq Dannunzio d'Annunzio
* !eq Carlotta 'carlotta
* !eq Dante 'dante
* !eq Mario 'mario
-o
{ tamara    STamara  LTamara 
* luisa     SLuisa   LLuisa
* emilia    SEmilia  LEmilia
* finzi     SFinzi   LFinzi
* despiga   SDeSpiga LDeSpiga
* aelis     SAelis   LAelis
* dannunzio SDannunzio LDannunzio 
* carlotta  SCarlotta  LCarlotta
* dante     SDante     LDante 
* mario     SMario     LMario
}.
